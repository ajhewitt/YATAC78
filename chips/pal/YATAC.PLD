Name     YATAC ;
PartNo   00 ;
Date     9/1/2019 ;
Revision 07 ;
Designer A J Hewitt ;
Company  ;
Assembly None ;
Location None ;
Device   g16v8a ;

/****************************** INPUT PINS *******************************/
PIN 1      = sclk          ; /*                                          */
PIN 2      = !reset        ; /*                                          */
PIN 3      = pclk          ; /*                                          */
PIN 4      = hblank        ; /*                                          */
PIN 5      = DD7           ; /*                                          */
PIN [6..9] = [I4..7]       ; /*                                          */

/****************************** OUTPUT PINS ******************************/
PIN 12    = pcent          ; /* PC Count Enable (ent)                    */
PIN 13    = !prog          ; /* Program Select (0=prog)                  */
PIN 14    = !fetch         ; /* CPU execution state (0=fetch, 1=execute) */
PIN 15    = hsel           ; /* H Register Select (0=L 1=H)              */
PIN 16    = boot           ; /* Boot mode (0=run, 1=booting)             */
PIN 17    = sign           ; /* Cached value of DD7                      */
PIN 18    = PA16           ; /* ROM A16                                  */
PIN 19    = PA17           ; /* ROM A17                                  */

aluhl = !I7 & !I6 & !I5;
alul  = !I7 & !I6 &  I5;
aluh  =        I6;
rom   = !I7 &  I6 &  I5;
oper  =  I7 & !I6;
nop   =  oper     & !I5 & !I4;
ld    =  oper     & !I5 &  I4;
ldc   =  oper     &  I5;

skip  = ldc & (I4 $ sign);
start = ldc & I4 & !fetch;

pcent = (pclk & (fetch # oper)) # skip;
prog  = fetch # oper;
PA16  = (pclk & !(hsel & aluhl)) # (!pclk & hblank);
!PA17 = pclk & (fetch # oper # rom);

FIELD execution = [!fetch, !hsel];
$DEFINE fetch_L   'b'00
$DEFINE fetch_H   'b'01
$DEFINE alu_L     'b'11
$DEFINE alu_H     'b'10

SEQUENCE execution {
  PRESENT fetch_L
    IF nop # skip # reset NEXT fetch_L;
    IF !I7 NEXT fetch_H;
    DEFAULT NEXT alu_H;
  PRESENT fetch_H
    IF reset NEXT fetch_L;
    IF aluh NEXT alu_H;
    DEFAULT NEXT alu_L;
  PRESENT alu_L
    IF reset NEXT fetch_L;
    IF aluhl NEXT alu_H;
    DEFAULT NEXT fetch_L;
  PRESENT alu_H
    NEXT fetch_L;
}

$DEFINE neg       'b'1
$DEFINE pos       'b'0

SEQUENCE sign {
  PRESENT neg
    IF !fetch & !oper & !DD7 NEXT pos;
    DEFAULT NEXT neg;
  PRESENT pos
    IF !fetch & !oper & DD7 NEXT neg;
    DEFAULT NEXT pos;
}

$DEFINE init      'b'1
$DEFINE run       'b'0

SEQUENCE boot {
  PRESENT init
    IF reset NEXT init;
    IF start NEXT run;
    DEFAULT NEXT init;
  PRESENT run
    IF reset NEXT init;
    DEFAULT NEXT run;
}
